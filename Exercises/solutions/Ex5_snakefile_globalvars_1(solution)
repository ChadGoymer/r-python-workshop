# This example snakefile uses global variables to define the top level all rule through the use of 'expand' 

# Here only one location is given in a list so the result is the same as before, but now if the list grows,
# so too will the list of input files in all. 

# The previous equivalent code for the rule is showed commented out below the new rule

# GLOBAL VARIABLES
LOCATIONS = ["UK-London"]


# This is the first rule run
rule all:
    input:
        expand("plots/{location}.png", location=LOCATIONS)
        # 'plots/UK-London.png'


# Produce plots -----------------------------------
rule plot:
    input:
        'data/aggregate/{location}.csv'
    output:
        'plots/{location}.png'
    shell:
        'Rscript plot_timeseries.R {input} {output}'


# Aggregate --------------------------------------
rule aggregate:
    input:
        'data/processed/{location}--20150701.csv',
        'data/processed/{location}--20150702.csv'
    output:
        'data/aggregate/{location}.csv'
    shell:
        'python aggregate_all.py {input} {output}'


# Processing --------------------------------------
rule process:
	input: 
		'data/raw/{dataset}.json'
	output:
		'data/processed/{dataset}.csv'
	shell:
		'python process_one.py {input} {output}'


# Download raw data ------------------------------
rule download:
    output:
        'data/raw/{loc}--{date}.json'
    shell:
    	'python source.py {wildcards.loc} {wildcards.date}'
